!-----------------------------------------------------------------------
module p__init
!-----------------------------------------------------------------------
  use c__eprm,  only: lx,lxa1,npa1
  use v__in,    only: q,tpi,pi
  use mtmod
  use p__initmg,only: initmg,delx
  implicit none
!
  private
  public :: init
!
  contains
!-----------------------------------------------------------------------
!-----------------------------------------------------------------------
  subroutine init( xx,vp1,pm1,lor,bf,ef,pbr,pind,alt,fce,lat,hh )
!-----------------------------------------------------------------------
    real(kind=8)   ,intent(out) ::  xx(npa1),vp1(3,npa1)
    real(kind=8)   ,intent(out) ::  pm1(3,npa1),lor(npa1)
    real(kind=8)   ,intent(out) ::  bf(3,lxa1),ef(3,lxa1),pbr(lxa1)
    real(kind=8)   ,intent(out) ::  alt(lxa1),fce(lxa1),lat(lxa1),hh(lxa1)
    integer(kind=4),intent(out) ::  pind(npa1)
!
    real(kind=8),parameter ::  kb = 1.381d-23 , vc = 2.9979d8
    real(kind=8),parameter ::  ev = 1.16d4 , me = 9.11d-31
!
    real(kind=8) ::  alp,phs,T,vpp,aa,factp
    integer      ::  ii,iseed
!-----------------------------------------------------------------------
!-----------------------------------------------------------------------
    factp = 180.d0/pi
!-----------------------------------------------------------------------
    iseed = 634
    call sgrnd(iseed)
!-----------------------------------------------------------------------
    ef(:,:) = 0.d0
!-----------------------------------------------------------------------
    call initmg( bf,pbr,alt,fce,lat,hh )
!-----------------------------------------------------------------------
!-----------------------------------------------------------------------
    T = 1.d3*ev
!-----------------------------------------------------------------------
    aa = me*vc**2/( kb*T + me*vc**2 )
    vpp = sqrt(1.d0 - aa**2)
!-----------------------------------------------------------------------
    do ii=1,npa1
!
      alp = dble(ii)*10.d0/factp
      phs = tpi*grnd()
!
      xx(ii) = 3.d0*delx
!
      vp1(1,ii) = vpp*cos(alp)
      vp1(2,ii) = vpp*sin(alp)*cos(phs)
      vp1(3,ii) = vpp*sin(alp)*sin(phs)
!
      lor(ii) = 1.d0/sqrt(1.d0-vp1(1,ii)**2-vp1(2,ii)**2-vp1(3,ii)**2)
      pm1(1,ii) = vp1(1,ii)*lor(ii)
      pm1(2,ii) = vp1(2,ii)*lor(ii)
      pm1(3,ii) = vp1(3,ii)*lor(ii)
!
      pind(ii) = 0
!
    end do
!-----------------------------------------------------------------------
!-----------------------------------------------------------------------
    return
!-----------------------------------------------------------------------
  end subroutine init
!-----------------------------------------------------------------------
!-----------------------------------------------------------------------
end module p__init
